
%start SkillLines
%title "Skill grammar"
%comment "スキル本文のパーサ"

%%

SkillLines
    : Line Period;

Line
    : ChangeDropStmtIncGenRandomDrop
    | ChangeAllOfBoradStmt
    | GenRandomDropStmt1
    | GenShapeStmt
    | DropRefreshStmt
    ;

ChangeDropStmtIncGenRandomDrop
    : Drops Wo Drop Ni { Camma ChangeDropBlockOtherFirst } WordChange
    // 変換と衝突するためここで拾う
    // 火を5個生成 , 水と回復を6個ずつ生成
    // 頭に`ランダムで`は本来のStmtで拾える。
    | Drops Wo Quantity WordGen
    | Drops WordOther From Drops Wo Quantity WordGen
    ;

ChangeAllOfBoradStmt
    : AllDrops Wo Drops Ni WordChange;

GenRandomDropStmt1
    : RandomSuffix Drops Wo Quantity WordGen;

GenShapeStmt
    : GenShapeBlock { Camma GenShapeBlock } WordChange;

DropRefreshStmt
    : RandomSuffix WordDrop Wo WordReplace;

// 木ドロップ を 水ドロップ に, 光ドロップ を 回復ドロップ に
//                          ~~~~~~~~~~~~~~~~~~~~~~~~
ChangeDropBlockOtherFirst
    : Drops Wo Drop Ni
    ;

GenShapeBlock
    : GSStartPosition Wo Drop Ni;

GSStartPosition
    : GSSPSide
    | GSSPCenter
    ;

GSSPSide
    : Position [ WordHorizon ] GenShapeNumOfGen [ And Position [ WordHorizon ] GenShapeNumOfGen ];

GSSPCenter
    : GSSPCenterBlocks [ No ] [ WordVertical | WordHorizon ] GenShapeNumOfGen;

GSSPCenterBlocks
    : GSSPCenterBlock [ And GSSPCenterBlock ];

GSSPCenterBlock
    : PositionLRTB From PosInt [ WordCol | WordRow ] WordLook;

Position
    : PositionLR GSSPSideWriteWidth
    | PositionTB
    ;

GSSPSideWriteWidth
    : WordSide [ WordVertical ]
    | WordVertical
    ;

PositionLRTB
    : WordLeft
    | WordRight
    | WordTop
    | WordBottom
    ;

PositionLR
    : WordLeft
    | WordRight
    | WordLeftAndRight
    ;

PositionTB
    : [ WordMost ] PositionTOrB WordRow;

PositionTOrB
    : WordTop
    | WordBottom
    ;

// 横、縦生成で共通
GenShapeNumOfGen
    : PosInt WordCol;

Drops
    : Drop { ManyDrop }
    | FiveAttribute { Plus Drop }
    | Drop And Drop
    ;

ManyDrop
    : Camma Drop
    | Plus Drop
    ;

AllDrops
    : WordAll WordDrop;

FiveAttribute
    : WordFiveAttribute [ WordDrop ];

Quantity
    : PosInt WordCount [ Each ];

Drop
    : Color [ WordDrop ]
    | NonColoredDrop [ WordDrop ];

NonColoredDrop
    : Recovery
    | Disturb
    | Bomb
    | Poison
    | DeadlyPoison
    ;

Color
    : Fire
    | Water
    | Wood
    | Lightning
    | Dark
    ;

RandomSuffix
    : WordRandom So;

Fire         : '火';
Water        : '水';
Wood         : '木';
Lightning    : '光';
Dark         : '闇';
Recovery     : '回復';
Disturb      : 'お邪魔';
Bomb         : '爆弾';
DeadlyPoison : '猛毒';
Poison       : '毒';

WordChange        : '変化';
WordDrop          : 'ドロップ';
WordAll           : '全';
WordFiveAttribute : '5属性';
WordRandom        : 'ランダム';
WordReplace       : '入れ替える';
WordCount         : '個';
WordGen           : '生成';
WordOther         : '以外';
WordVertical      : '縦';
WordHorizon       : '横';
WordRow           : '段';
WordCol           : '列';
WordSide          : '端';
WordLeftAndRight  : '両';
WordLeft          : '左';
WordRight         : '右';
WordTop           : '上';
WordBottom        : '下';
WordMost          : '最';
WordLook          : '目';


Wo     : 'を';
Ni     : 'に';
No     : 'の';
So     : 'で';
And    : 'と';
Each   : 'ずつ';
From   : 'から';
Plus   : '+';
Camma  : '、';
Period : '。';

PosInt : "[1-9]([0-9])*";