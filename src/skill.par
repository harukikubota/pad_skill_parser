
%start SkillLines
%title "Skill grammar"
%comment "スキル本文のパーサ"

%%

SkillLines
    : Line Period;

Line
    : GroupOfDropChange
    | DropRefreshStmt
    | DropUnLockStmt
    | TurnsOfApplyStmt
    ;

GroupOfDropChange
    : [ DropUnlockBlock ] [ StartsWithDropLine | GenRandomDropStmt | GenShapeStmt ]
    | ChangeDropWithDropUnlockLine
    ;

ChangeDropWithDropUnlockLine
    : AllDrops [ DropUnlockBlock | Wo ]
        [ Drops Ni WordChange [Si Camma GenRandomDropBlock WordGen]
        | GenShapeStmt
        // `全ドロップを強化`のみヒット
        | WordPowerUp
        // `全ドロップをロック`のみヒット
        | WordLock
        ];

StartsWithDropLine
    : Drops Wo Drop Ni { Camma ChangeDropBlockOtherFirst } WordChange
    | Drops Wo RandomSuffix Drops Ni WordChange
    // 変換と衝突するためここで拾う
    // 火を5個生成 , 水と回復を6個ずつ生成
    // 頭に`ランダムで`は本来のStmtで拾える。
    | Drops Wo Quantity { Camma GenRandomDropBlock } WordGen
    | Drops WordOther From GenRandomDropBlock WordGen
    | Drops Wo [ WordPowerUp | WordLock ]
    ;

GenRandomDropStmt
    : RandomSuffix GenRandomDropBlock { Camma GenRandomDropBlock } WordGen;

GenRandomDropBlock
    : Drops Wo Quantity;

GenShapeStmt
    : GenShapeBlock [ WordChange | WordGen ];

DropRefreshStmt
    : RandomSuffix WordDrop Wo WordReplace;

DropUnLockStmt
    : WordDrop No WordLock WordState Wo WordRelease;

TurnsOfApplyStmt
    : Turns TurnsOfApplyBlock;

// 木ドロップ を 水ドロップ に, 光ドロップ を 回復ドロップ に
//                          ~~~~~~~~~~~~~~~~~~~~~~~~
ChangeDropBlockOtherFirst
    : Drops Wo Drop Ni
    ;

GenShapeBlock
    : GenShapeBlockRowCol { Camma GenShapeBlockRowCol }
    | GenShapeBlockOtherRowCol
    ;

GenShapeBlockRowCol
    : GSStartPosition Wo Drop Ni;

GenShapeBlockOtherRowCol
    : ShapeType Ni Drops Wo [ Quantity ]
    | ShapeType Wo Drops Ni
    ;

GSStartPosition
    : GSSPSide
    | GSSPCenter
    ;

GSSPSide
    : Position [ WordHorizon ] GenShapeNumOfGen [ And Position [ WordHorizon ] GenShapeNumOfGen ];

GSSPCenter
    : GSSPCenterBlocks [ No ] [ WordVertical | WordHorizon ] GenShapeNumOfGen;

GSSPCenterBlocks
    : GSSPCenterBlock [ And GSSPCenterBlock ];

GSSPCenterBlock
    : PositionLRTB From PosInt [ WordCol | WordRow ] WordLook;

Position
    : PositionLR GSSPSideWriteWidth
    | PositionTB
    ;

GSSPSideWriteWidth
    : WordSide [ WordVertical ]
    | WordVertical
    ;

PositionLRTB
    : WordLeft
    | WordRight
    | WordTop
    | WordBottom
    ;

PositionLR
    : WordLeft
    | WordRight
    | WordLeftAndRight
    ;

PositionTB
    : [ WordMost ] PositionTOrB WordRow;

PositionTOrB
    : WordTop
    | WordBottom
    ;

// 横、縦生成で共通
GenShapeNumOfGen
    : PosInt WordCol;

ShapeType
    : ShapeOfL
    | [ OnBoard ] ShapeOfZ
    | ShapeOfCross
    | Size No ShapeOfSquare
    | ShapeOfBoardPerimeter
    | ShapeOfBoardCenter
    | ShapeOfBoardTop
    | ShapeOfBoardBottom
    | ShapeOfBoardCorners
    | ShapeOfSpiderweb
    | ShapeOfCrescentMoon
    | [ OnBoard ] ShapeOfOblique
    // 現在では `7の形`のみ それ以外は改修必要
    | PosInt ShapeOfSomeKind
    ;

DropUnlockBlock
    // [ドロップの]?ロックを解除し、
    // 単色変換、ランダム生成用
    : [ [WordDrop] No ] WordLock Wo WordRelease Si Camma;

Drops
    : Drop { ManyDrop }
    | FiveAttribute { Plus Drop }
    | Drop And Drop
    ;

ManyDrop
    : Camma Drop
    | Plus Drop
    ;

AllDrops
    : WordAll WordDrop;

FiveAttribute
    : WordFiveAttribute [ WordDrop ];

Quantity
    : PosInt WordCount [ Each ];

Drop
    : Color [ WordDrop ]
    | NonColoredDrop [ WordDrop ];

NonColoredDrop
    : Recovery
    | Disturb
    | Bomb
    | Poison
    | DeadlyPoison
    ;

Color
    : Fire
    | Water
    | Wood
    | Lightning
    | Dark
    ;

RandomSuffix
    : WordRandom De;

Size
    : PosInt Multi PosInt;

OnBoard
    : WordBoard WordTop Ni;

Turns
    // Nターンの間、
    : PosInt WordTurn No WordBetween Camma;

TurnsOfApplyBlock
    : DropsEasierToFalloff
    | PowerupDropsEasierToFalloff
    ;

DropsEasierToFalloff
    : Drops [ Camma WordPowerUp WordDrop ] [ Ga ] [ SkillVolumeVariation ] [ WordFalloff | WordFall ];

PowerupDropsEasierToFalloff
    : WordPowerUp WordDrop [ And Drops ] Ga
        [ PosInt Percent No WordProbability De WordFall
        | WordLittle WordFalloff
        ]
    ;

SkillVolumeVariation
    : WordLittleMore
    | WordLittle
    | WordOnly
    ;

Fire         : '火';
Water        : '水';
Wood         : '木';
Lightning    : '光';
Dark         : '闇';
Recovery     : '回復';
Disturb      : 'お邪魔';
Bomb         : '爆弾';
DeadlyPoison : '猛毒';
Poison       : '毒';

ShapeOfL              : 'L字型';
ShapeOfZ              : 'Z字型';
ShapeOfCross          : '十字型';
ShapeOfSquare         : '正方形';
ShapeOfBoardPerimeter : '盤面外周';
ShapeOfBoardCenter    : '盤面中央';
ShapeOfBoardTop       : '盤面上部';
ShapeOfBoardBottom    : '盤面下部';
ShapeOfBoardCorners   : '盤面4隅';
ShapeOfSpiderweb      : '蜘蛛の巣状';
ShapeOfCrescentMoon   : '三日月状';
ShapeOfOblique        : '斜め';
ShapeOfSomeKind       : 'の形';

WordChange        : '変化';
WordDrop          : 'ドロップ';
WordAll           : '全';
WordFiveAttribute : '5属性';
WordRandom        : 'ランダム';
WordReplace       : '入れ替える';
WordCount         : ['個' | 'つ'];
WordGen           : '生成';
WordOther         : '以外';
WordVertical      : '縦';
WordHorizon       : '横';
WordRow           : '段';
WordCol           : '列';
WordSide          : '端';
WordLeftAndRight  : '両';
WordLeft          : '左';
WordRight         : '右';
WordTop           : '上';
WordBottom        : '下';
WordMost          : '最';
WordLook          : '目';
WordBoard         : '盤面';
WordRelease       : '解除';
WordLock          : 'ロック';
WordState         : '状態';
WordPowerUp       : '強化';
WordTurn          : 'ターン';
WordBetween       : '間';
WordLittleMore    : 'ほんの少し';
WordLittle        : '少し';
WordOnly          : 'のみ';
WordFalloff       : '落ちやすくなる';
WordFall          : '落ちてくる';
WordProbability   : '確率';

Wo     : 'を';
Ni     : 'に';
No     : 'の';
De     : 'で';
And    : 'と';
Si     : 'し';
Ga     : 'が';
Each   : 'ずつ';
From   : 'から';

Camma  : '、';
Period : '。';

Plus    : '+';
Multi   : '×';
Percent : '%';

PosInt : "[1-9]([0-9])*";